buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath "no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.1.0"
  }
}

repositories {
  mavenCentral()
  jcenter()
  mavenLocal()
}


ext.aurora = [
    requireStaging: false,
    applyNexusRepositores: false,
    applyDeliveryBundleConfig: false,
    applyAsciiDocPlugin: false,
    groovyVersion: "2.4.7"
]

apply plugin: 'signing'
apply plugin: 'no.skatteetaten.plugins.aurora'

group = 'no.skatteetaten.aurora.gradle.plugins'

dependencies {
  compile(
      gradleApi(),
      'com.github.zafarkhaja:java-semver:0.9.0',
      'commons-io:commons-io:2.5'
  )
}

install.dependsOn << build


task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

if (ext.has("ossrhUsername") && ext.has("ossrhPassword")) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Aurora Docker Plugin'
          packaging 'jar'
          description 'A plugin that is used to build docker image using gradle.'
          url 'https://github.com/Skatteetaten/aurora-docker-plugin'

          scm {
            connection 'scm:git:https://github.com/Skatteetaten/aurora-docker-plugin.git'
            developerConnection 'scm:git:https://github.com/Skatteetaten/aurora-docker-plugin.git'
            url 'https://github.com/Skatteetaten/aurora-docker-plugin'
          }

          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id 'bjartek'
              name 'Bjarte S. Karlsen'
              email 'Bjarte.Karlsen@Skatteetaten.no'
            }
          }
        }
      }
    }
  }
}



task wrapper(type: Wrapper) {
  gradleVersion = "3.0"
}
